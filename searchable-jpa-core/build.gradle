apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

ext {
    set('springBootVersion', '3.2.5')
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

// Version-specific Jakarta EE/Javax configuration
configurations.all {
    if (project.version.toString().startsWith('1.')) {
        // 1.x versions: Jakarta EE 9+ enforced
        resolutionStrategy {
            force 'jakarta.persistence:jakarta.persistence-api:3.1.0'
            force 'jakarta.validation:jakarta.validation-api:3.0.2'
            force 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
        }

        // Exclude all javax persistence and validation
        exclude group: 'javax.persistence', module: 'javax.persistence-api'
        exclude group: 'javax.validation', module: 'validation-api'
        exclude group: 'org.hibernate', module: 'hibernate-core'
        exclude group: 'org.hibernate', module: 'hibernate-entitymanager'
    } else if (project.version.toString().startsWith('0.1.')) {
        // 0.1.x versions: Javax.* enforced
        resolutionStrategy {
            force 'javax.persistence:javax.persistence-api:2.2'
            force 'javax.validation:validation-api:2.0.1.Final'
            force 'org.hibernate.validator:hibernate-validator:6.2.5.Final'
        }

        // Exclude Jakarta EE dependencies
        exclude group: 'jakarta.persistence', module: 'jakarta.persistence-api'
        exclude group: 'jakarta.validation', module: 'jakarta.validation-api'
    }
}

dependencies {
    if (project.version.toString().startsWith('1.')) {
        // 1.x versions: Jakarta EE API dependencies
        implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
        implementation 'jakarta.validation:jakarta.validation-api:3.0.2'

        // Hibernate Validator for Jakarta - Spring Boot 3.x compatible
        implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'

        // OpenAPI/Swagger - Spring Boot 3.x compatible version
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.6.0'
    } else if (project.version.toString().startsWith('0.1.')) {
        // 0.1.x versions: Javax.* API dependencies
        implementation 'javax.persistence:javax.persistence-api:2.2'
        implementation 'javax.validation:validation-api:2.0.1.Final'

        // Hibernate Validator for Javax - Spring Boot 2.x compatible
        implementation 'org.hibernate.validator:hibernate-validator:6.2.5.Final'

        // OpenAPI/Swagger - Spring Boot 2.x compatible version
        implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
        implementation 'org.springdoc:springdoc-openapi-webmvc-core:1.7.0'
    }

    // Spring Boot starters (will use appropriate versions based on springBootVersion)
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'

    // Other dependencies
    implementation 'org.modelmapper:modelmapper:3.2.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.h2database:h2'
    testImplementation 'com.microsoft.sqlserver:mssql-jdbc'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
}

test {
    useJUnitPlatform {
        // Exclude performance tests from regular test runs
        excludeTags 'performance'
    }
    
    // Always run tests, never consider them up-to-date
    outputs.upToDateWhen { false }
    
    // Pass system properties to test JVM
    systemProperties = System.properties
    
    // Enable passing specific system properties
    if (System.getProperty('spring.profiles.active')) {
        systemProperty 'spring.profiles.active', System.getProperty('spring.profiles.active')
    }
    
    // Clean test results before running
    doFirst {
        delete fileTree(dir: "${buildDir}/test-results", include: '**/*')
        delete fileTree(dir: "${buildDir}/reports/tests", include: '**/*')
    }
}

// Performance test task - run separately
task performanceTest(type: Test) {
    group = 'verification'
    description = 'Run performance tests manually'
    
    useJUnitPlatform {
        includeTags 'performance'
    }
    
    // Pass system properties to test JVM
    systemProperties = System.properties
    
    // Enable passing specific system properties
    if (System.getProperty('spring.profiles.active')) {
        systemProperty 'spring.profiles.active', System.getProperty('spring.profiles.active')
    }
    
    // Always run performance tests, never consider them up-to-date
    outputs.upToDateWhen { false }
    
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

compileJava.inputs.files(processResources)

compileJava {
    options.compilerArgs << '-parameters'
}

// Task to generate test data
task generateLargeData(type: JavaExec) {
    group = 'data'
    description = 'Generate large test dataset (5M records)'
    classpath = sourceSets.test.runtimeClasspath
    main = 'dev.simplecore.searchable.test.fixture.CsvTestDataGenerator'
    args = ['large']
}

task generateSmallData(type: JavaExec) {
    group = 'data'
    description = 'Generate small test dataset (10K records)'
    classpath = sourceSets.test.runtimeClasspath
    main = 'dev.simplecore.searchable.test.fixture.CsvTestDataGenerator'
    args = ['small']
}