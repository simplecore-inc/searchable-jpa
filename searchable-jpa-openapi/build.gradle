apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

// Spring Boot version is managed by root project

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

// Version-specific configuration for Spring Boot 3.x vs 2.x compatibility
configurations.all {
    if (project.version.toString().startsWith('1.')) {
        // 1.x versions: Spring Boot 3.x - exclude Javax dependencies
        exclude group: 'javax.persistence', module: 'javax.persistence-api'
        exclude group: 'javax.validation', module: 'validation-api'
        exclude group: 'javax.annotation', module: 'javax.annotation-api'
    } else if (project.version.toString().startsWith('0.1.')) {
        // 0.1.x versions: Spring Boot 2.x - exclude Jakarta dependencies
        exclude group: 'jakarta.persistence', module: 'jakarta.persistence-api'
        exclude group: 'jakarta.validation', module: 'jakarta.validation-api'
        exclude group: 'jakarta.annotation', module: 'jakarta.annotation-api'
    }
}

dependencies {
    api project(':searchable-jpa-core')

    if (project.version.toString().startsWith('1.')) {
        // 1.x versions: Spring Boot 3.5.x compatible OpenAPI
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui'
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api'
    } else if (project.version.toString().startsWith('0.1.')) {
        // 0.1.x versions: Spring Boot 2.x compatible OpenAPI
        implementation 'org.springdoc:springdoc-openapi-ui'
        implementation 'org.springdoc:springdoc-openapi-webmvc-core'
    }

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
}

test {
    useJUnitPlatform()
}

compileJava.inputs.files(processResources)

