plugins {
    id 'org.springframework.boot' version '3.5.5' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'java'
    id 'maven-publish'
}

ext {
    springBootVersion = '3.5.5'
    springDependencyManagementVersion = '1.1.7'
    
    // Dependency versions - only for dependencies not managed by Spring Boot
    versions = [
        // SpringDoc OpenAPI versions (not managed by Spring Boot)
        springdocOpenapiStarter: '2.5.0',
        springdocOpenapiLegacy: '1.7.0',
        
        // Other dependencies not managed by Spring Boot
        modelmapper: '3.2.0',
        mssqlJdbc: '12.4.2.jre11'
    ]
}

allprojects {
    group = 'dev.simplecore.searchable'
    version = '1.0.6'

    System.setProperty("spring.boot.version.check.enabled", "false")

    // Version compatibility validation
    afterEvaluate {
        def versionStr = version.toString()

        if (versionStr.startsWith('1.')) {
            // 1.x versions: Spring Boot 3.2.x+ required
            if (!springBootVersion.startsWith('3.')) {
                throw new GradleException("Version ${version} requires Spring Boot 3.2.x+, but current version is ${springBootVersion}")
            }
        } else if (versionStr.startsWith('0.1.')) {
            // 0.1.x versions: Spring Boot 2.7.x required
            if (!springBootVersion.startsWith('2.7')) {
                throw new GradleException("Version ${version} requires Spring Boot 2.7.x, but current version is ${springBootVersion}")
            }
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
        
        dependencies {
            // SpringDoc OpenAPI versions (not managed by Spring Boot)
            dependency "org.springdoc:springdoc-openapi-starter-webmvc-ui:${versions.springdocOpenapiStarter}"
            dependency "org.springdoc:springdoc-openapi-starter-webmvc-api:${versions.springdocOpenapiStarter}"
            dependency "org.springdoc:springdoc-openapi-ui:${versions.springdocOpenapiLegacy}"
            dependency "org.springdoc:springdoc-openapi-webmvc-core:${versions.springdocOpenapiLegacy}"
            
            // Other dependencies not managed by Spring Boot
            dependency "org.modelmapper:modelmapper:${versions.modelmapper}"
            dependency "com.microsoft.sqlserver:mssql-jdbc:${versions.mssqlJdbc}"
        }
    }

    apply plugin: 'maven-publish'
    
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                
                // Resolve version conflicts for Maven publishing
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }

                pom {
                    def metadata = [
                        'searchable-jpa-core': [
                            name: 'Searchable JPA Core',
                            description: 'Core library for Searchable JPA'
                        ],
                        'searchable-jpa-openapi': [
                            name: 'Searchable JPA OpenAPI',
                            description: 'OpenAPI/Swagger support for Searchable JPA'
                        ],
                        'spring-boot-starter-searchable-jpa': [
                            name: 'Spring Boot Starter for Searchable JPA',
                            description: 'Spring Boot Starter for Searchable JPA'
                        ]
                    ]
                    if (metadata.containsKey(project.name)) {
                        def projectMetadata = metadata[project.name]
                        setName(projectMetadata['name'])
                        setDescription(projectMetadata['description'])
                    } else {
                        throw new RuntimeException("No metadata found for project: ${project.name}")
                    }
                    url = 'https://github.com/simplecore-inc/searchable-jpa'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'thkwag'
                            name = 'Taehwan Kwag'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/simplecore-inc/searchable-jpa.git'
                        developerConnection = 'scm:git:ssh://github.com:simplecore-inc/searchable-jpa.git'
                        url = 'https://github.com/simplecore-inc/searchable-jpa'
                    }

                    // Add version compatibility information (properties)
                    withXml {
                        def properties = asNode().get('properties')
                        if (properties.isEmpty()) {
                            properties = asNode().appendNode('properties')
                        }

                        // Spring Boot version compatibility information
                        if (project.version.toString().startsWith('1.')) {
                            properties.appendNode('spring-boot.version.required', '[3.2.0,4.0.0)')
                            properties.appendNode('jakarta.version.required', '9+')
                            properties.appendNode('javax.version.supported', 'false')
                        } else if (project.version.toString().startsWith('0.1.')) {
                            properties.appendNode('spring-boot.version.required', '[2.7.0,3.0.0)')
                            properties.appendNode('jakarta.version.supported', 'false')
                            properties.appendNode('javax.version.required', 'true')
                        }
                    }
                }
            }
        }
        
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/simplecore-inc/searchable-jpa")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
                }
            }
        }
    }
    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs.addAll(['-parameters', "-Xlint:-deprecation", "-Xlint:-options"])
    }

    test {
        useJUnitPlatform()
        
        // Always run tests, never consider them up-to-date
        outputs.upToDateWhen { false }
        
        // Clean test results before running
        doFirst {
            delete fileTree(dir: "${buildDir}/test-results", include: '**/*')
            delete fileTree(dir: "${buildDir}/reports/tests", include: '**/*')
        }
    }
}

wrapper {
    gradleVersion = '8.5'
    distributionType = Wrapper.DistributionType.BIN
}