apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

sourceCompatibility = '17'
targetCompatibility = '17'

ext {
    set('springBootVersion', '3.2.5')
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

// Version-specific Jakarta EE/Javax configuration
configurations.all {
    if (project.version.toString().startsWith('1.')) {
        // 1.x versions: Jakarta EE 9+ enforced
        resolutionStrategy {
            force 'jakarta.persistence:jakarta.persistence-api:3.1.0'
            force 'jakarta.validation:jakarta.validation-api:3.0.2'
        }

        // Exclude all javax persistence and validation
        exclude group: 'javax.persistence', module: 'javax.persistence-api'
        exclude group: 'javax.validation', module: 'validation-api'
    } else if (project.version.toString().startsWith('0.1.')) {
        // 0.1.x versions: Javax.* enforced
        resolutionStrategy {
            force 'javax.persistence:javax.persistence-api:2.2'
            force 'javax.validation:validation-api:2.0.1.Final'
        }

        // Exclude Jakarta EE dependencies
        exclude group: 'jakarta.persistence', module: 'jakarta.persistence-api'
        exclude group: 'jakarta.validation', module: 'jakarta.validation-api'
    }
}

dependencies {
    if (project.version.toString().startsWith('1.')) {
        // 1.x versions: Jakarta EE API dependencies
        implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
        implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'

        // Spring Boot 3.x compatible OpenAPI
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.6.0'
    } else if (project.version.toString().startsWith('0.1.')) {
        // 0.1.x versions: Javax.* API dependencies
        implementation 'javax.persistence:javax.persistence-api:2.2'
        implementation 'javax.annotation:javax.annotation-api:1.3.2'

        // Spring Boot 2.x compatible OpenAPI
        implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
        implementation 'org.springdoc:springdoc-openapi-webmvc-core:1.7.0'
    }

    api project(':searchable-jpa-core')
    api project(':searchable-jpa-openapi')

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.slf4j:slf4j-api'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
}

test {
    useJUnitPlatform()
}

compileJava.inputs.files(processResources)
